CODE SEGMENT PUBLIC 'CODE'

ASSUME CS:CODE,DS:DATA,SS:STACK	

	ORG 400H			;避开中断向量表，0000:0000-0000:03FF
	
START:	MOV AX,DATA
		MOV DS,AX
		MOV AX,STACK
		MOV SS,AX
		MOV AX,TOP
		MOV SP,AX
		CLI				;IF=0，禁止可屏蔽中断
		
		;设置中断向量IR1
		PUSH DS
		MOV AX,0
		MOV DS,AX
		MOV BX,21H*4	;ICW2高5位00100，IR1对应低3位001,00100001B
		MOV AX,OFFSET IR1
		MOV [BX],AX
		MOV AX,0
		MOV [BX+2],AX
		POP DS
		
		;第一片8255A初始化
		MOV AL,80H		;1000 0000B,1000 A口输出，工作方式0，0000 B口输出，工作方式0，C端口输出
		MOV DX,CT1_PORT
		OUT DX,AL
		MOV AL,00H
		MOV DX,A1_PORT
		OUT DX,AL
		MOV DX,B1_PORT
		OUT DX,AL
		MOV DX,C1_PORT
		OUT DX,AL

		;第二片8255A初始化
		MOV AL,90H		;1001 0000B,1000 A口输入，工作方式0，0000 B口输出，工作方式0，C端口输出
		MOV DX,CT2_PORT
		OUT DX,AL
		MOV AL,00H
		MOV DX,A2_PORT
		OUT DX,AL
		MOV DX,B2_PORT
		OUT DX,AL
		MOV DX,C2_PORT
		OUT DX,AL

		;8259初始化
		MOV DX,CS8259A 	;写ICW1（写入偶地址端口A0=0，接入A0的是A1）
		MOV AL,ICW1
		OUT DX,AL
		MOV DX,CS8259B 	;写ICW2（写入奇地址端口A0=1，接入A0的是A1）
		MOV AL,ICW2
		OUT DX,AL
		MOV AL,ICW4		;写ICW4
		OUT DX,AL
		MOV AL,OCW1		;写OCW1（普通屏蔽方式，写入奇地址端口）
		OUT DX,AL
		STI        		;IF=1，允许可屏蔽中断
	
		;8253初始化，时钟输入频率是1MHZ（通过计数器0和计数器1级联实现）周期为1000000/1000/1000=1s
		MOV DX,CTL		;0号计数器写入控制字
		MOV AL,00110110B;0号计数器，先写低八位再写高八位，工作方式3，二进制计数
		OUT DX,AL
		MOV DX,CNT0		;0号计数器写入初值1000
		MOV AX,1000		;先写低八位再写高八位
		OUT DX,AL
		MOV AL,AH
		OUT DX,AL
		MOV DX,CTL		;1号计数器写入控制字
		MOV AL,01110110B;1号计数器，先写低八位再写高八位,工作方式3，二进制计数
		OUT DX,AL
		MOV DX,CNT1		;1号计数器写入初值1000
		MOV AX,1000		;先写低八位再写高八位
		OUT DX,AL
		MOV AL,AH
		OUT DX,AL
		
		
MAIN:	MOV DX,A1_PORT
		MOV AL,00010001B;常态:机动车道绿灯，人行道红灯
		OUT DX,AL

LP:		NOP
		JMP LP

;按钮边沿触发式中断
IR1:   	CLI
		PUSH AX
		PUSH BX
		PUSH CX
		PUSH DX
		
		CALL COUNT1
		
		;绿色倒计时牌不显示
		MOV DX,B2_PORT
		MOV AL,0
		OUT DX,AL
		
		MOV DX,C2_PORT
		MOV AL,0
		OUT DX,AL
		
		MOV DX,A1_PORT
		MOV AL,00100100B;过街情况:机动车道红灯，人行道绿灯
		OUT DX,AL
		
		CALL COUNT2
		MOV DX,CS8259A 	;对A0=0端口写入中断服务子程序结束EOI命令
		MOV AL,20H		;EOI命令，命令字20H，，对正在服务的ISR复位，保证能再次触发
		OUT DX,AL
		CLI
		POP DX
		POP CX
		POP BX
		POP AX
		OUT DX,AL
		
		;恢复中断前信号灯颜色及指示牌状态
		MOV DX,A1_PORT
		MOV AL,00010001B;常态:机动车道绿灯，人行道红灯
		OUT DX,AL
		
		MOV DX,B1_PORT
		MOV AL,0
		OUT DX,AL
		
		MOV DX,C1_PORT
		MOV AL,0
		OUT DX,AL
		
		STI
		IRET

;39S绿色倒计时
COUNT1 PROC
		MOV SI,6
		MOV CX,4
		
A1:		MOV AL,TAB[SI]	;十位数循环
		MOV AH,0
		MOV DX,B2_PORT
		OUT DX,AX
		INC SI
		PUSH SI
		PUSH CX
		MOV SI,0
		MOV CX,10
		
C1:		MOV AL,TAB[SI]	;个位数循环
		MOV AH,0
		MOV DX,C2_PORT
		OUT DX,AX
		CALL DELAY
		INC SI
		LOOP C1
		
		POP CX
		POP SI
		LOOP A1
		RET
COUNT1 ENDP

;19S红色倒计时
COUNT2 PROC
		MOV SI,8
		MOV CX,2

A2:		MOV AL,TAB[SI]	;十位数循环
		MOV AH,0
		MOV DX,B1_PORT
		OUT DX,AX
		INC SI
		PUSH SI
		PUSH CX
		MOV SI,0
		MOV CX,10
		
C2:		MOV AL,TAB[SI]	;个位数循环
		MOV AH,0
		MOV DX,C1_PORT
		OUT DX,AX
		CALL DELAY
		INC SI
		LOOP C2
		
		POP CX
		POP SI
		LOOP A2
		RET
COUNT2 ENDP

;延迟函数（LOOP计数）
DELAY PROC
	PUSH AX
	PUSH DX
	PUSH CX
	MOV CX,10000
L1:	NOP
	LOOP L1
	MOV CX,10000
L2:	NOP
	LOOP L2
	MOV CX,10000
L3:	NOP
	LOOP L3
	POP CX
	POP DX
	POP AX
	RET
DELAY ENDP

;延迟函数（8253计数）
; DELAY PROC
	; PUSH AX
; T:	MOV DX,CNT0		;0号计数器写入初值1000
	; MOV AX,1000		;先写低八位再写高八位
	; OUT DX,AL
	; MOV AL,AH
	; OUT DX,AL
	; MOV DX,CNT1		;1号计数器写入初值1000
	; MOV AX,1000		;先写低八位再写高八位
	; OUT DX,AL
	; MOV AL,AH
	; OUT DX,AL
	; MOV DX,A2_PORT
	; IN AL,DX
	; AND AL,00000001B
	; TEST AL,00000001B
	; JNZ T
	; POP AX
	; RET
; DELAY ENDP


CODE ENDS

DATA SEGMENT
    ;Y0--8259，ADR15高电平（对应74LS138的E1），ADR12，ADR13，ADR14低电平，对应000，即Y0
    CS8259A EQU 8000H	;1000 0000 0000 0000B,ADR1低电平，偶地址A0是0
    CS8259B EQU 8002H	;1000 0000 0000 0010B,ADR1高电平，奇地址A0是1
    ICW1 EQU 00010011B	;边沿触发中断，单独使用（故无ICW3），设置IC4
    ICW2 EQU 00100000B	;中断类型码高5位0
    ICW4 EQU 00000101B	;全嵌套方式，非缓冲方式，主片，非自动结束中断方式（AEOI模式，需发送EOI中断结束命令）
    OCW1 EQU 11111101B	;OCW1控制字接收IR1，屏蔽其他IR（OCW1是普通屏蔽方式）
    
	;Y1--8253，ADR15高电平（对应74LS138的E1），ADR12高电平，ADR13和ADR14低电平，对应001，即Y1
    CNT0 EQU 9000H		;ADR15……ADR0：1001 0000 0000 0000，ADR1低电平，ADR2低电平
    CNT1 EQU 9002H		;ADR15……ADR0：1001 0000 0000 0010，ADR1高电平，ADR2低电平
    CNT2 EQU 9004H		;ADR15……ADR0：1001 0000 0000 0100，ADR1低电平，ADR2高电平
    CTL  EQU 9006H		;ADR15……ADR0：1001 0000 0000 0110，ADR1高电平，ADR2高电平
    
	;Y2--8255，ADR15高电平（对应74LS138的E1），ADR12和ADR14低电平，ADR13高电平，对应010，即Y2
    A1_PORT EQU 0A000H	;ADR19……ADR0：0000 1010 0000 0000 0000，ADR1低电平，ADR2低电平
    B1_PORT EQU 0A002H	;ADR19……ADR0：0000 1010 0000 0000 0010，ADR1高电平，ADR2低电平
    C1_PORT EQU 0A004H	;ADR19……ADR0：0000 1010 0000 0000 0100，ADR1低电平，ADR2高电平
    CT1_PORT EQU 0A006H	;ADR19……ADR0：0000 1010 0000 0000 0110，ADR1高电平，ADR2高电平
	
	;Y3--8255，ADR15高电平（对应74LS138的E1），ADR12和ADR13高电平，ADR14低电平，对应011，即Y3
    A2_PORT EQU 0B000H	;ADR19……ADR0：0000 1011 0000 0000 0000，ADR3低电平，ADR4低电平
    B2_PORT EQU 0B008H	;ADR19……ADR0：0000 1011 0000 0000 1000，ADR3高电平，ADR4低电平
    C2_PORT EQU 0B010H	;ADR19……ADR0：0000 1011 0000 0001 0000，ADR3低电平，ADR4高电平
    CT2_PORT EQU 0B018H	;ADR19……ADR0：0000 1011 0000 0001 1000，ADR3高电平，ADR4高电平
    
	;7seg（7段数码表）共阴，9-0
    TAB DB 6FH,7FH,07H,7DH,6DH,66H,4FH,5BH,06H,3FH
	;0-9
	;TAB DB 3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH
DATA ENDS

STACK SEGMENT STACK
    STA DB 1024H DUP(?)
    TOP EQU $-STA
STACK ENDS


END START